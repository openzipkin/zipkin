apply plugin: 'com.github.johnrengelman.shadow'
ext.mainClassName = 'com.twitter.zipkin.aggregate.RunHadoopJob'

if (!project.hasProperty("hosts")) {
    ext.hosts = "localhost"
}

if (!project.hasProperty("port")) {
    ext.port = "9160"
}

task run(type:JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    workingDir project.buildDir
    // only cassandra is supported
    args '--source', "cassandra", '--hosts', hosts, '--port', port
}

jar.manifest.attributes 'Main-Class': mainClassName

tasks.build.dependsOn(shadowJar)
artifacts.zipkinUpload shadowJar

repositories {
    // For dependencies of zipkin-cassandra
    maven { url 'http://conjars.org/repo' }
}

dependencies {
    compile project(':zipkin-scrooge')
    compile "com.twitter:util-app_${scalaInterfaceVersion}:${commonVersions.twitterUtil}"
    compile "org.apache.hadoop:hadoop-client:2.4.0"
    compile "com.twitter:scalding-core_${scalaInterfaceVersion}:0.15.0"
    // @fixme cassandra aggregate stuff needs to be rewritten to use CqlInputFormat
    //compile project(':zipkin-cassandra')
}
