apply plugin: 'com.github.johnrengelman.shadow'
ext.mainClassName = 'com.twitter.zipkin.collector.Main'

task run(type:JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    // When configKey == "dev", run out of the query's directory to reuse zipkin.db
    workingDir configKey == "dev" ? project(':zipkin-query-service').buildDir : project.buildDir
    // See gradle.properties for an explanation of configKey
    args '-f', "${projectDir}/config/collector-${configKey}.scala"
}

jar.manifest.attributes 'Main-Class': mainClassName

tasks.build.dependsOn(shadowJar)
artifacts.zipkinUpload shadowJar
configurations.zipkinUpload.artifacts.removeAll { it.archiveTask.is jar }

shadowJar {
    exclude 'META-INF/LICENSE' // jackson's META-INF/LICENSE conflicts with the directory META-INF/license
}

dependencies {
    compile "com.twitter:finagle-ostrich4_${scalaInterfaceVersion}:${commonVersions.finagle}"
    compile "com.twitter:finagle-thriftmux_${scalaInterfaceVersion}:${commonVersions.finagle}"
    compile "ch.qos.logback:logback-core:${commonVersions.logback}"
    compile "ch.qos.logback:logback-classic:${commonVersions.logback}"

    compile project(':zipkin-receiver-kafka')
    compile project(':zipkin-receiver-scribe')
    compile project(':zipkin-cassandra')
    compile project(':zipkin-anormdb')
    compile anormDriverDependencies["mysql"] // for query-mysql
}

sourceSets.main.resources.srcDirs += ['config']
apply from: "${rootDir}/gradle/idea-mark-config-dir-as-resource.gradle"
apply from: "${rootDir}/gradle/verify-licenses.gradle"
