// Adopted from https://github.com/ratpack/ratpack/blob/master/gradle/dependencies.gradle

ext {
    scalaVersion = '2.11.7'
    scalaInterfaceVersion = '2.11'

    commonVersions = [
            finagle: '6.30.0',
            guava: '16.0.1', // to match finagle
            scrooge: '4.2.0',
            twitterUtil: '6.29.0',
            twitterServer: '1.15.0',
            finatra: '2.1.1',
            jackson: '2.4.4', // from finatra's build.sbt
            ostrich: '9.13.0',
            griddle: '1.7',
            slf4j: '1.7.12',
            logback: '1.1.3'
    ]

    // Database drivers
    // The keys are the valid values for the system property dbEngine.
    // The values are the dependency to be used in the build to support the requested storage system.
    //
    // The default value of dbEngine is sqlite-persistent, defined in gradle.properties
    // To override, run the build like this: ./gradlew build -PdbEngine=postgresql
    anormDriverDependencies = [
            "sqlite-memory": "org.xerial:sqlite-jdbc:3.8.11.2",  // sqlite-jdbc4 isn't out, yet
            "sqlite-persistent": "org.xerial:sqlite-jdbc:3.8.11.2",
            "h2-memory": "com.h2database:h2:1.4.190",
            "h2-persistent": "com.h2database:h2:1.4.190",
            // Don't add EOL versions! http://www.postgresql.org/support/versioning
            "postgresql": "org.postgresql:postgresql:9.3-1104-jdbc4",
            "mysql": "mysql:mysql-connector-java:5.1.37"
    ]
}

subprojects { subproject ->
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                // Ensures libthrift 0.5.0 isn't accidentally in the classpath
                if (details.requested.group == 'org.apache.thrift') {
                  details.useVersion '0.9.0' // https://github.com/twitter/scrooge/issues/203
                }
                // Don't use mixed versions of SLF4J or Logback
                if (details.requested.group == 'org.slf4j') {
                  details.useVersion commonVersions.slf4j
                }
                if (details.requested.group == 'ch.qos.logback') {
                  details.useVersion commonVersions.logback
                }
            }
        }

        // Some dependencies early bound slf4j impl
        exclude module: "slf4j-log4j12"
        exclude module: "slf4j-logback"
     }
}
