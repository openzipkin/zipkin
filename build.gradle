import org.gradle.util.VersionNumber

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.3.1'
    id 'net.researchgate.release' version '2.2.1'
}

ext.isSnapshot = VersionNumber.parse(rootProject.version).qualifier == "SNAPSHOT"

allprojects { thisProject ->
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: "com.jfrog.artifactory-upload"

    configurations {
        zipkinUpload
    }

    // Following are workarounds to idea goal failing on:
    // Cannot infer Scala class path because..
    // - no repository is declared in root project
    repositories { jcenter() }

    // - no Scala library Jar was found
    apply from: "$rootDir/gradle/dependencies.gradle"
    dependencies {
        compile "org.scala-lang:scala-library:${scalaVersion}"
    }

    task sourceJar(type: Jar) {
      description "An archive of the source code for Maven Central"
      classifier "sources"
      from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
      description "An archive of the JavaDocs for Maven Central"
      classifier "javadoc"
      from javadoc
    }

    artifacts {
        zipkinUpload jar, javadocJar, sourceJar
    }

    bintray {
        // Read variables from travis encrypted config or local
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')

        // Workaround: we don't want to publish anything from the root project.
        // https://github.com/bintray/gradle-bintray-plugin/issues/74
        if (thisProject == rootProject) {
            configurations = []
        } else {
            configurations = ['zipkinUpload']
        }

        dryRun = System.getenv('BINTRAY_DRYRUN') != null
        publish = true

        pkg {
            licenses = ['Apache-2.0']
            repo = System.getenv('BINTRAY_ZIPKIN_REPO') ?: "zipkin"
            vcsUrl = 'https://github.com/openzipkin/zipkin.git'
            name = System.getenv('BINTRAY_ZIPKIN_PACKAGE') ?: "zipkin"
            userOrg = System.getenv('BINTRAY_ZIPKIN_ORG') ?: "openzipkin"
            version {
                name = "${project.version}"
                desc = "Zipkin version ${project.version}"
                released  = new Date()
                vcsTag = "${project.version}"

                gpg {
                    sign = true
                }
            }
        }
    }

    // Workaround: we don't want to publish anything from the root project.
    if (thisProject == rootProject) {
        artifactoryPublish.publishConfigs()
    }

    thisProject.plugins.withType(DistributionPlugin) {
		thisProject.tasks.distTar {
			compression = Compression.GZIP
			extension = 'tar.gz'
		}
    }
}

release {
    failOnCommitNeeded = false
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = failOnSnapshotDependencies
    failOnUpdateNeeded = true
    revertOnFail = true
    preCommitText = '[RELEASE] - '
    preTagCommitMessage = 'publish version: '
    newVersionCommitMessage = 'new version: '

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
        pushToCurrentBranch = true
    }
}

artifactory {
    contextUrl = "http://oss.jfrog.org/artifactory"

    publish {
        repository {
            if (project.isSnapshot) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
            maven = true
        }

        defaults {
            publishConfigs 'zipkinUpload'
        }
    }
}

task createReleaseTag(overwrite: true, group: 'Release') {
    // Zipkin is released by pushing tags manually, so
    // we need the release plugin to NOT create tags when it runs.
}

task runBuildTasks(overwrite: true, group: 'Release') {
    // During the release process, building and testing in the Travis job running `gradle release`
    // is redundant. We can save time by running the build and tests only on commits to master.
}

subprojects { subproject ->
    apply from: "$rootDir/gradle/scalatest.gradle"

    compileTestScala {
        scalaCompileOptions.additionalParameters = ["-feature"]
    }

    compileScala {
        scalaCompileOptions.additionalParameters = ["-feature"]
    }

    test {
        maxParallelForks Runtime.runtime.availableProcessors()
    }

    repositories {
        maven { url 'http://repo.typesafe.com/typesafe/releases/' }
        maven { url 'https://maven.twttr.com/' }
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.6.4' // libthrift 0.5 otherwise pins 1.5.x

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-all:1.10.19'
        testCompile "org.scalatest:scalatest_${scalaInterfaceVersion}:2.2.5"
    }
    // SBT migration note: end of zipkinSettings
}

// Quick hack to visualize inter-project dependencies
task dependencyReport {
    description "Write the dependency graph of the zipkin-* projects in this repository into project-dependencies.dot for use with GraphViz."
    doLast {
        def file = new File("project-dependencies.dot")
        file.delete()
        file << "digraph {\n"
        file << "splines=ortho\n"
        rootProject.childProjects.each { item ->
            def from = item.value
            from.configurations.compile.dependencies
                    .matching { it in ProjectDependency }
                    .each { to -> file << ("\"${from.name}\" -> \"${to.name}\"\n")}
        }
        file << "}\n"
    }
}

task zipkinUpload(group: 'release') {
    description "Upload to oss.jfrog.org if this is a SNAPSHOT release. Upload to both Bintray and oss.jfrog.org otherwise."
    dependsOn artifactoryPublish
    if (!project.isSnapshot) {
        dependsOn bintrayUpload
    }
}


