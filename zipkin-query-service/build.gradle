apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
mainClassName = 'com.twitter.zipkin.query.Main'

run {
    // When configKey == "dev", run out of the collector's directory to reuse zipkin.db
    workingDir configKey == "dev" ? project(':zipkin-collector-service').buildDir : project.buildDir
    // See gradle.properties for an explanation of configKey
    args '-f', "${projectDir}/config/query-${configKey}.scala"
}

tasks.build.dependsOn(shadowJar)
artifacts.zipkinUpload shadowJar
artifacts.zipkinUpload distTar

repositories {
    // For dependencies of zipkin-cassandra
    maven { url 'http://conjars.org/repo' }
}

dependencies {
    compile project(':zipkin-query')
    compile project(':zipkin-cassandra')
    compile project(':zipkin-redis')
    compile project(':zipkin-anormdb')
}

sourceSets.main.resources.srcDirs += ['config']
apply from: "${rootDir}/gradle/idea-mark-config-dir-as-resource.gradle"

