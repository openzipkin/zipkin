apply plugin: 'com.moowork.node'

artifacts.zipkinUpload jar

node {
    // Version of node to use.
    version = '5.5.0'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}")
}

task npmBuild(type: NpmTask) {
    args = ['run-script', 'build']
    dependsOn npmInstall
}
processResources.dependsOn(npmBuild)

task npmTest(type: NpmTask) {
    args = ['run-script', 'test']
    dependsOn npmBuild
}
check.dependsOn npmTest

task npmLint(type: NpmTask) {
    args = ['run-script', 'lint']
    dependsOn npmInstall
}
check.dependsOn npmLint

install.repositories.mavenInstaller.pom*.whenConfigured {pom ->
    pom.dependencies.clear()
}
